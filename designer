30 MODE 8
40 PAPER 0
50 setup
100 REPeat loop
140 key=KEYROW(1)
150 SELect ON key
170 =2
180 movecursor -1,0
190 =4
200 movecursor 0,1
210 =16
220 movecursor 1,0
230 =128
240 movecursor 0,-1
250 =8
260 sav
270 =1
280 fillcolour
290 =64
300 changecolour
310 END SELect 
320 key=KEYROW(0)
340 SELect ON key
360 =2:reverse
370 =8:ldchar
380 =16:shift
400 END SELect 
800 END REPeat loop
999 STOP
1500 DEFine PROCedure setup
1510 WINDOW#2,512,256,0,0
1520 PAPER#2,0:CLS#2
1530 cposx=0:cposy=0
1540 colour=1
1542 OPEN#3,scr:WINDOW#3,200,80,290,120
1544 PRINT#3;"f1      :reverse"
1546 PRINT#3;"f2      :load character"
1548 PRINT#3;"f3      :shift character to right"
1550 WINDOW#1,274,200,10,10:BORDER#1,1,1
1560 WINDOW#0,500,44,10,212:BORDER#0,1,1
1570 INPUT#0;"size of character (x,y)";x,y
1580 base=RESPR(2000)
1590 FOR f=0 TO y*x/2:POKE base+f,0:NEXT f
1595 INPUT#0;"load a character?";q$
1600 CLS
1610 FILL 0
1620 grid x,y
1625 IF q$=="y" THEN ldchar
1630 drawcursor 0,0
1640 END DEFine 
2000 DEFine PROCedure grid(x,y)
2020 gapx=100/x:gapy=100/y
2040 INK 7
2120 FOR yy=0 TO 100 STEP gapy
2140 LINE 0,yy TO 100,yy
2160 NEXT yy
2200 FOR xx=0 TO 100 STEP gapx
2220 LINE xx,0 TO xx,100
2230 NEXT xx
2240 END DEFine grid
3000 DEFine PROCedure drawcursor
3005 POINT cposx*gapx,cposy*gapy
3010 LINE_R 1,1 TO gapx-2,0 TO 0,gapy-2 TO -gapx+2,0 TO 0,-gapy+2
3020 END DEFine 
3100 DEFine PROCedure movecursor(left,right)
3110 INK 0
3120 drawcursor
3130 INK 7
3140 cposx=cposx+left:cposy=cposy+right
3145 IF cposx>x-1 THEN cposx=0
3146 IF cposx<0 THEN cposx=x-1
3147 IF cposy>y-1 THEN cposy=0
3148 IF cposy<0 THEN cposy=y-1
3150 drawcursor
3190 END DEFine 
3400 DEFine PROCedure fillcolour
3410 LOCal G,RB,byte
3420 G=colour DIV 4
3430 byte=base+(cposx DIV 4)*2+(y-cposy-1)*x/2
3432 sadd=(137572+(y-cposy)*128+(cposx DIV 4)*2)
3435 POKE byte,PEEK(byte) && (~~(3*(2^(2*(3-(cposx MOD 4))))))
3437 POKE sadd,PEEK(sadd) && (~~(3*(2^(2*(3-(cposx MOD 4))))))
3440 POKE byte,PEEK(byte) || G*(2^(2*(3-cposx MOD 4)+1))
3445 POKE sadd,PEEK(sadd) || G*(2^(2*(3-cposx MOD 4)+1))
3450 RB=colour MOD 4
3455 POKE byte+1,PEEK(byte+1) && (~~(3*(2^(2*(3-(cposx MOD 4))))))
3457 POKE sadd+1,PEEK(sadd+1) && (~~(3*(2^(2*(3-(cposx MOD 4))))))
3460 POKE byte+1,PEEK(byte+1) || RB*(2^(2*(3-(cposx MOD 4))))
3465 POKE sadd+1,PEEK(sadd+1) || RB*(2^(2*(3-(cposx MOD 4))))
3505 FILL 1
3507 INK colour
3510 drawcursor
3530 FILL 0
3540 END DEFine 
3600 DEFine PROCedure changecolour
3605 REPeat l
3610 INPUT#0,"colour number?";colour
3620 IF colour>=0 AND colour<=7 THEN EXIT l
3640 END REPeat l
3680 END DEFine 
4000 DEFine PROCedure sav
4005 INPUT#0,"filename:";f$
4010 SBYTES f$,base,y*x/2
4020 END DEFine 
4300 DEFine PROCedure ldchar
4320 INPUT#0;"filename:";name$
4340 LBYTES  name$,base
4350 display
4360 END DEFine 
4370 DEFine PROCedure display
4380 FOR cposx=0 TO x-1
4400 FOR cposy=0 TO y-1
4420 byte=base+(cposx DIV 4)*2 +(y-cposy-1)*x/2
4426 sadd=(137572+(y-cposy)*128+(cposx DIV 4)*2)
4440 G=((PEEK(byte) && (3*(2^(2*(3-(cposx MOD 4)))))) DIV ((4^(3-(cposx MOD 4)))))*2
4450 POKE sadd,PEEK(sadd) && (~~(3*(2^(2*(3-(cposx MOD 4))))))
4455 POKE sadd,PEEK(sadd) || (G*(2^(2*(3-cposx MOD 4)))/2)
4460 RB=(PEEK(byte+1) && (3*(2^(2*(3-(cposx MOD 4)))))) DIV (4^(3-(cposx MOD 4)))
4470 POKE sadd+1,PEEK(sadd+1) && (~~(3*(2^(2*(3-(cposx MOD 4))))))
4475 POKE sadd+1,PEEK(sadd+1) || RB*(2^(2*(3-(cposx MOD 4))))
4480 col=G+RB
4500 INK col
4510 FILL 1
4520 drawcursor
4530 FILL 0
4540 NEXT cposy
4560 NEXT cposx
4600 END DEFine 
4800 DEFine PROCedure shift
4820 CALL a,0,3,0,0,0,0,0,base,base
4840 display
4860 END DEFine 
